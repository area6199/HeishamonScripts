on System#Boot then
    print("$$ System Boot");
    setTimer(1,30);
end

on timer=1 then
    print("$$ on timer=1");
    controlHeatRequest();
    setTimer(1,15);
end

on controlHeatRequest then
    print("$$ controlHeatRequest called");

    $compressorFreq   = @Compressor_Freq;
    $operatingMode    = @Operating_Mode_State;
    $heatRequest      = @Z1_Heat_Request_Temp;
    $mainOutletTemp   = @Main_Outlet_Temp;
    $mainTargetTemp   = @Main_Target_Temp;
    $outsideTemp      = @Outside_Temp;

    print("$$ controlHeatRequest values:");
    print(" compressorFreq: ");   print($compressorFreq);
    print(", operatingMode: ");   print($operatingMode);
    print(", heatRequest: ");     print($heatRequest);
    print(", mainOutletTemp: ");  print($mainOutletTemp);
    print(", mainTargetTemp: ");  print($mainTargetTemp);
    print(", outsideTemp: ");     print($outsideTemp);

    $minHeatRequest     = -2;
    $nightHeatRequest   = -5;
    $maxHeatRequest     = 5;
    $compressorLowLimit = 22;
    $tempHighDelta      = 1.6;
    $tempLowDelta       = 0.5;

    if $operatingMode == 0 && $outsideTemp > 0 then
        if $compressorFreq == 0 && $heatRequest > $minHeatRequest && $heatRequest < $maxHeatRequest then
            print("$$ point 1 change heatRequest to "); print($minHeatRequest);
            $heatRequest = $minHeatRequest;
        elseif $compressorFreq < $compressorLowLimit && $mainOutletTemp > ($tempHighDelta + $mainTargetTemp) && $heatRequest < $maxHeatRequest then
            print("$$ point 2 change heatRequest to "); print($heatRequest + 1);
            $heatRequest = $heatRequest + 1;
        elseif $mainOutletTemp < ($tempLowDelta + $mainTargetTemp) && $heatRequest > $minHeatRequest && $heatRequest < $maxHeatRequest then
            print("$$ point 3 change heatRequest to "); print($heatRequest - 1);
            $heatRequest = $heatRequest - 1;
        end
    end

    if (%hour >= 22 || %hour < 4) && $compressorFreq == 0 && $outsideTemp > 5 then
        print("$$ night active, change heatRequest to "); print($nightHeatRequest);
        $heatRequest = $nightHeatRequest;
    end

    if (%hour >= 4 && %hour < 22) && $compressorFreq == 0 then
        print("$$ day active, change heatRequest to "); print($minHeatRequest);
        $heatRequest = $minHeatRequest;
    end

    if $heatRequest != @Z1_Heat_Request_Temp then
        print("$$ change SetZ1HeatRequestTemperature to "); print($heatRequest);
        @SetZ1HeatRequestTemperature = $heatRequest;
    end

    if $outsideTemp >= 5 then
        if @Quiet_Mode_Level != 3 then
            print("$$ set QuietMode to 3");
            @SetQuietMode = 3;
        end
    end

    if $outsideTemp < 5 then
        if @Quiet_Mode_Level != 0 then
            print("$$ set QuietMode to 0");
            @SetQuietMode = 0;
        end
    end
end
